# Adapted from https://gist.github.com/pstuifzand/4447349
:start ::= json
json ::= object | array

object ::= '{' members '}' action => do_object
members ::= pair* proper => 1 separator => comma
pair ::= string ':' value

value ::= string
  | object
  | number action => do_number
  | array
  | true action => do_true
  | false action => do_false
  | null action => do_null

array ::= '[' elements ']' action => do_array
elements ::= value* proper => 1 separator => comma

comma ~ ','

string ::= '"' in_string '"' action => do_string
in_string ~ /([^"\\]|\\[\"\\\/bftnrt]|\\u[a-fA-F0-9]{4})+/

number ~ /-?([\d]+)(\.\d+)?([eE][+-]?\d+)?/

true ~ 'true'
false ~ 'false'
null ~ 'null'

:discard ~ whitespace
whitespace ~ [\s]+